plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.darkmatterservers'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'net.dv8tion:JDA:5.6.1'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
}

tasks.test {
    useJUnitPlatform()
}

// Disable default JAR in favor of Shadow
tasks.jar {
    enabled = false
}

tasks.shadowJar {
    archiveClassifier.set("") // Use this as the main artifact
    mergeServiceFiles()

    manifest {
        attributes(
                'Main-Class': 'com.darkmatterservers.EclipseBytes' // make sure this class has main()!
        )
    }
}

afterEvaluate {
    tasks.named("publishMavenJavaPublicationToMavenLocal") {
        dependsOn tasks.shadowJar
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = 'eclipsebytes'
            version = project.version

            artifact tasks.shadowJar
            artifact tasks.sourcesJar
        }
    }

    repositories {
        mavenLocal()
    }
}
